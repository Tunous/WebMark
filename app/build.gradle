apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def signingPropertiesFile = rootProject.file('signing/signing.properties')
def useLocalSigning = signingPropertiesFile.exists()
Properties signingProperties = null

if (useLocalSigning) {
    signingProperties = new Properties()
    signingProperties.load(signingPropertiesFile.newDataInputStream())
}

android {
    buildToolsVersion Config.buildTools
    compileSdkVersion Config.compileSdk

    defaultConfig {
        minSdkVersion Config.minSdk
        targetSdkVersion Config.targetSdk
        applicationId "me.thanel.webmark"
        versionCode 1
        versionName "0.1.0"
        testInstrumentationRunner "me.thanel.webmark.test.WebMarkRunner"
    }

    compileOptions {
        sourceCompatibility Config.javaVersion
        targetCompatibility Config.javaVersion
    }

    signingConfigs {
        release {
            if (useLocalSigning) {
                storeFile rootProject.file('signing/app-release.jks')
                keyAlias signingProperties.getProperty('keyAlias')
                storePassword signingProperties.getProperty('storePassword')
                keyPassword signingProperties.getProperty('keyPassword')
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            versionNameSuffix '-debug'
            applicationIdSuffix '.debug'
        }
    }

    packagingOptions {
        exclude 'META-INF/library_release.kotlin_module'
    }

    testOptions {
        animationsDisabled = true
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation project(':libraries:database')

    kapt Deps.glideCompiler

    implementation Deps.androidxLegacy
    implementation Deps.androidxAppCompat
    implementation Deps.constraintLayout
    implementation Deps.coroutinesAndroid
    implementation Deps.coroutinesCore
    implementation Deps.crux
    implementation Deps.espressoIdlingResource
    implementation Deps.glide
    implementation Deps.jsoup
    implementation Deps.kodein
    implementation Deps.kodeinAndroid
    implementation Deps.kotlin
    implementation Deps.kotpref
    implementation Deps.ktx
    implementation Deps.lifecycle
    implementation Deps.lifecycleViewModel
    implementation Deps.material
    implementation Deps.materialPopupMenu
    implementation Deps.okhttp
    implementation Deps.recyclerView
    implementation Deps.recyclerViewUtils
    implementation Deps.timber
    implementation Deps.work

    testImplementation Deps.junit

    androidTestImplementation Deps.androidxArchCoreTesting
    androidTestImplementation Deps.espresso
    androidTestImplementation Deps.espressoContrib
    androidTestImplementation Deps.espressoIntents
    androidTestImplementation Deps.sqlDelight
    androidTestImplementation Deps.testRules
    androidTestImplementation Deps.androidxTestRunner
    androidTestImplementation Deps.workTesting
}
